(define (delay-it exp env)
 (list 'thunk exp env))
(define (thunk? exp)
 (tagged-list? exp 'thunk))
(define (thunk-exp thunk) (cadr thunk))
(define (thunk-env thunk) (caddr thunk))
(define (delay-it-no-memo exp env)
 (list 'thunk-no-memo exp env))
(define (thunk-no-memo? obj)
 (tagged-list? obj 'thunk-no-memo))
(define (evaluated-thunk? obj)
 (tagged-list? obj 'evaluated-thunk))
(define (thunk-value evaluated-thunk) (cadr evaluated-thunk))
(define (force-it obj)
 (cond ((thunk? obj)
        (let ((result (actual-value
                       (thunk-exp obj)
                       (thunk-env obj))))
         (set-car! obj 'evaluated-thunk)
         (set-car! (cdr obj) result)
         (set-cdr! (cdr obj) '())
         result))
        ((evaluated-thunk? obj) (thunk-value obj))
        ((thunk-no-memo? obj) (actual-value (thunk-exp obj)
                                            (thunk-env obj)))
        (else
         obj)))
